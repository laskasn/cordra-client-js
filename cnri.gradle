apply plugin: 'maven'

plugins.withId('java') {
    if (project.hasProperty('refreshSnapshots')) {
        configurations.all { resolutionStrategy { cacheChangingModulesFor 0, 'seconds' } }
    }

    if (project.hasProperty('seeJavaWarnings')) {
        compileJava { options.compilerArgs += [ '-Xlint:unchecked', '-Xlint:deprecation' ] }
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    if (JavaVersion.current().isJava9Compatible()) {
        compileJava {
            options.compilerArgs.addAll(['--release', '8'])
        }
    }

    // remove Gradle 4 warnings
    compileJava {
        options.compilerArgs += '-proc:none'
    }

    defaultTasks 'clean', 'jar'
    compileJava.mustRunAfter clean
    jar.dependsOn clean

    plugins.withId('war') {
        defaultTasks 'clean', 'war'
        war.dependsOn clean

        jar {
            classifier 'classes'
        }
        artifacts {
            archives jar
        }
    }
}

def repoBaseUri = project.ext.properties.repoBaseUri
if (repoBaseUri && !repoBaseUri.endsWith('/')) repoBaseUri = repoBaseUri + '/'
def mavenUser = project.ext.properties.mavenUser
def mavenPassword = project.ext.properties.mavenPassword

repositories {
    if (project.hasProperty('mavenCentralStaging')) {
       project.ext.properties.mavenCentralStaging.split(',').each { stagingUrl ->
           maven {
               url stagingUrl
               credentials {
                   username ossrhUsername
                   password ossrhPassword
               }
           }
       }
       jcenter()
    } else if (repoBaseUri) {
        maven {
            url repoBaseUri + 'releases'
            credentials {
                username mavenUser
                password mavenPassword
            }
        }
        maven {
            url repoBaseUri + 'snapshots'
            credentials {
                username mavenUser
                password mavenPassword
            }
        }
        maven {
            url repoBaseUri + 'mirror'
            credentials {
                username mavenUser
                password mavenPassword
            }
        }
    } else {
        maven {
            url "file://${rootProject.projectDir}/cnri-libs"
        }
        flatDir {
            dirs 'lib', '.', 'WEB-INF/lib'
        }
        jcenter()
    }
}

configurations.all {
    resolutionStrategy.eachDependency { details ->
        if (details.requested.group == 'commons-logging' && details.requested.name == 'commons-logging') {
            details.useTarget group: 'org.slf4j', name: 'jcl-over-slf4j', version: '1.7.30'
        } else if (details.requested.group == 'commons-logging' && details.requested.name == 'commons-logging-api') {
            details.useTarget group: 'org.slf4j', name: 'jcl-over-slf4j', version: '1.7.30'
        } else if (details.requested.group == 'log4j' && details.requested.name == 'log4j') {
            details.useTarget group: 'org.slf4j', name: 'log4j-over-slf4j', version: '1.7.30'
        } else if (details.requested.group == 'org.slf4j' && details.requested.name == 'slf4j-log4j12') {
            details.useTarget group: 'org.slf4j', name: 'log4j-over-slf4j', version: '1.7.30'
        }
    }
}

project.ext.populateConfigurationWithMavenArtifacts = { name, test ->
    configurations.each { config ->
        if (config.name == name) return
        if (!config.canBeResolved) return
        if (config.name != 'testCompileClasspath' && config.name != 'testRuntimeClasspath' &&
            config.dependencies.findAll(test).isEmpty()) return
        config.resolvedConfiguration.resolvedArtifacts.each { artifact ->
            def dep = artifact.moduleVersion.id
            if (test(dep)) {
                project.dependencies.add(name, [ group: dep.group, name: dep.name, version: dep.version, classifier: artifact.classifier, ext: artifact.extension ], { transitive = false})
                project.dependencies.add(name, [ group: dep.group, name: dep.name, version: dep.version, ext: 'pom' ], { transitive = false })
                if (artifact.extension == 'jar' || artifact.extension == 'war') {
                    project.dependencies.add(name, [ group: dep.group, name: dep.name, version: dep.version, classifier: 'sources' ], { transitive = false })
                }
            }
        }
    }
}

project.ext.configurationToMavenRepository = { name, path ->
    copySpec {
        configurations[name].resolvedConfiguration.resolvedArtifacts.collect { artifact ->
            def dep = artifact.moduleVersion.id
            from(artifact.file) {
                into(path + "/" + dep.group.replace(".","/") + "/" + dep.name + "/" + dep.version)
            }
        }
    }
}

plugins.withId('java') {
    tasks.withType(Javadoc) {
        // turn off Javadoc linting
        options.addStringOption('Xdoclint:none', '-quiet')
        if (JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html5', true)
        }
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }
}

project.ext.configurePom = { pom ->
    // packaging needs to be set late
    if (project.ext.properties.pomPackaging) {
        pom.whenConfigured { packaging = project.ext.pomPackaging }
    }
    pom.project {
        if (!pom.model.name) {
            def pomName = project.ext.properties.pomName
            if (!pomName) pomName = (project.group + ':' + project.archivesBaseName)
            name pomName
        }
        if (!pom.model.description) {
            def pomDescription = project.ext.properties.pomDescription
            if (!pomDescription) pomDescription = project.description
            if (pomDescription) {
                description pomDescription
            }
        }
        if (!pom.model.url && project.ext.properties.pomUrl) {
            url project.ext.properties.pomUrl
        }
        if (!pom.model.scm) {
            def pomScmUrl = project.ext.properties.pomScmUrl
            if (!pomScmUrl) pomScmUrl = project.ext.properties.pomUrl
            if (pomScmUrl) {
                scm {
                    url pomScmUrl
                }
            }
        }
        if (!pom.model.licenses && (project.ext.properties.pomLicenseName || project.ext.properties.pomLicenseUrl)) {
            licenses {
                license {
                    if (project.ext.properties.pomLicenseName) {
                        name project.ext.properties.pomLicenseName
                    }
                    if (project.ext.properties.pomLicenseUrl) {
                        url project.ext.properties.pomLicenseUrl
                    }
                }
            }
        }
        if (!pom.model.developers && project.ext.properties.pomDeveloperEmail) {
            developers {
                developer {
                    name 'CNRI'
                    email project.ext.properties.pomDeveloperEmail
                    organization = 'CNRI' // = required here
                    organizationUrl 'https://www.cnri.reston.va.us'
                }
            }
        }
    }
}

if (!project.hasProperty('mavenCentral')) {
    uploadArchives {
        repositories.mavenDeployer {
            repository(url: repoBaseUri + "releases") {
                authentication(userName: mavenUser, password: mavenPassword)
            }
            snapshotRepository(url: repoBaseUri + "snapshots") {
                authentication(userName: mavenUser, password: mavenPassword)
            }
            afterEvaluate {
                project.ext.configurePom(pom)
            }
        }
    }
} else {
    apply plugin: 'signing'

    signing {
        sign configurations.archives
    }

    project.tasks.withType(Sign) {
        signatures.all {
            if (it.file.name.endsWith('.tar.gz.asc')) {
                // Workaround for apparent Gradle bug around signing files where extension has a dot
                it.type = 'tar.gz.asc'
            }
        }
    }

    uploadArchives {
        repositories.mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }
            afterEvaluate {
                project.ext.configurePom(pom)
            }
        }
    }
}
